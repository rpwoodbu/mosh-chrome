package(default_visibility = ["//visibility:private"])

config_setting(
    name = "pnacl_mode",
    values = { "cpu": "pnacl" },
)

cc_library(
    name = "libssh",
    srcs = glob(
        include = ["src/**/*.c"],
        exclude = ["src/gssapi.c"],
    ),
    hdrs = glob([
        "include/libssh/*.h",
        "src/**/*.data",
    ]),
    includes = ["include"],
    deps = [
        ":config_lib",
    ] + select({
        ":pnacl_mode": [
            "@glibc_compat//:glibc_compat",
            "@openssl//:openssl",
            "@pnacl_zlib//:zlib",
        ],
        "//conditions:default": [
            ":local_openssl",
            ":local_zlib",
        ],
    }),
    copts = select({
        "pnacl_mode": [
            "-DPNACL=ON",
            "-DWITH_STATIC_LIB=ON",
            "-DWITH_SHARED_LIB=OFF",
        ],
        "//conditions:default": [
            "-DWITH_STATIC_LIB=OFF",
            "-DWITH_SHARED_LIB=ON",
        ],
    }) + [
        "-DWITH_ZLIB=OFF",
        "-DWITH_EXAMPLES=OFF",
        "-Wno-char-subscripts",
        "-Wno-implicit-function-declaration",
    ],
    visibility = ["//visibility:public"],
)

genrule(
    name = "config_file",
    outs = ["config.h"],
    cmd = """
        cat > $@ << EOF
#define PACKAGE "libssh"
#define VERSION "0.7.1"
#define HAVE_ARPA_INET_H 1
#define HAVE_SYS_TIME_H 1
#define HAVE_TERMIOS_H 1
#define HAVE_UNISTD_H 1
#define HAVE_OPENSSL_AES_H 1
#define HAVE_OPENSSL_BLOWFISH_H 1
#define HAVE_OPENSSL_DES_H 1
#define HAVE_OPENSSL_ECDH_H 1
#define HAVE_OPENSSL_EC_H 1
#define HAVE_OPENSSL_ECDSA_H 1
#define HAVE_PTHREAD_H 1
#define HAVE_OPENSSL_ECC 1
#define HAVE_ECC 1
#define HAVE_SNPRINTF 1
#define HAVE_VSNPRINTF 1
#define HAVE_ISBLANK 1
#define HAVE_STRNCPY 1
#define HAVE_GETADDRINFO 1
#define HAVE_POLL 1
#define HAVE_SELECT 1
#define HAVE_STRTOULL 1
#define HAVE_LIBCRYPTO 1
#define HAVE_PTHREAD 1
#define HAVE_GCC_THREAD_LOCAL_STORAGE 1
#define HAVE_MSC_THREAD_LOCAL_STORAGE 1
#define HAVE_GCC_NARG_MACRO 1
#define HAVE_COMPILER__FUNC__ 1
#define HAVE_COMPILER__FUNCTION__ 1
#define WITH_SFTP 1
#define WITH_SERVER 1
#define WITH_PCAP 1
#define DEBUG_CALLTRACE 1
EOF
    """,
)

cc_library(
    name = "config_lib",
    hdrs = [":config_file"],
)

cc_library(
    name = "local_openssl",
    linkopts = [
        "-lcrypto",
        "-lssl",
    ],
)

cc_library(
    name = "local_zlib",
    linkopts = ["-lz"],
)
